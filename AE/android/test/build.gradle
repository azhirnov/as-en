apply plugin: 'com.android.application'

android {
	namespace  'AE.test'
	compileSdk 34

	defaultConfig {
		applicationId 		'AE.Test'
		minSdkVersion 		24 // 24+ for vulkan
		targetSdkVersion 	34

		ndk {
			abiFilters "arm64-v8a", "armeabi-v7a" //, "x86", "x86_64"
		}
		externalNativeBuild {
			cmake {
				arguments '-DANDROID_STL=c++_static',  // see https://developer.android.com/ndk/guides/cpp-support#static_runtimes
						  '-DANDROID_ARM_NEON=ON',
						  '-DAE_ENABLE_VULKAN=ON'
				targets 'TestLauncher'
			}
		}
	}

	buildTypes {
		release {
			debuggable true // to enable logcat in release
			minifyEnabled = false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.debug
		}
	}

	ndkVersion "26.3.11579264"

	externalNativeBuild {
		cmake {
			version '3.22.1+'
			path	'../../CMakeLists.txt'
		}
	}
	sourceSets {
		main {
			assets.srcDirs = [	'../../../AE-Temp/engine/graphics',			// TODO: include only vulkan pipelines
								'../../../AE-Temp/engine/graphics_hl',
								'../../../AE-Temp/engine/perf_graphics' ]
		}
	}
	packagingOptions {
		jniLibs {
			excludes += ['lib/**/*.so']
			pickFirsts += ['lib/**/libTestLauncher.so',		'lib/**/libTestsBase.so',		'lib/**/libTestsScripting.so',
						   'lib/**/libTestsThreading.so',	'lib/**/libTestsNetworking.so',	'lib/**/libTestsSerializing.so',
						   'lib/**/libTestsPlatform.so',	'lib/**/libTestsGraphics.so',	'lib/**/libTestsGraphicsHL.so',
						   'lib/**/libTestsECS-st.so',		'lib/**/libTestsVFS.so',		'lib/**/libTestsHuLang.so',
						   'lib/**/libTestsAtlasTools.so',	'lib/**/libTestsGeometryTools.so',
						   'lib/**/libPerfBase.so',			'lib/**/libPerfThreading.so',	'lib/**/libPerfGraphics.so']
			pickFirsts += ['lib/**/libVkLayer_khronos_validation.so']
		}
		resources  {
			excludes += '**/*.kotlin_*'
		}
	}
}

dependencies {
	implementation project(path: ':engine')
}
